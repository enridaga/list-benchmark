prefix midi: <http://purl.org/midi-ld/midi#>
prefix midi-note: <http://purl.org/midi-ld/notes/>
prefix midi-prog: <http://purl.org/midi-ld/programs/>
prefix prov: <http://www.w3.org/ns/prov#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xml: <http://www.w3.org/XML/1998/namespace>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

WITH <%%DATASET%%>
DELETE
{
  	?elt a rdf:List .
	?elt rdf:first ?first .
	?elt rdf:rest ?after .
	?before rdf:rest ?elt . 
}
INSERT {
	?before rdf:rest ?after
}
WHERE {
	?elt rdf:first ?first .
	OPTIONAL { ?before rdf:rest ?elt } .
	OPTIONAL { ?elt rdf:rest ?after } .
	{{
		SELECT ?elt WHERE {{
			SELECT
				?elt (count(?mid) as ?i)
			WHERE {
			   <%%TRACK%%> 
			        midi:hasEvents ?events .
			   ?events rdf:rest* ?mid .
			   ?mid rdf:rest* ?elt .
			   ?elt rdf:first ?event
			} 
			GROUP BY ?elt
		}}
		ORDER BY ?i
		LIMIT 1
		OFFSET %%INDEX2%%
	}}
}

# VERSION WITH FILTER(?i = %%INDEX1%%) AND NO ORDER BY SAME PERFORMANCE!